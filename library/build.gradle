plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "java-library"
    id "maven-publish"
}

version = "1.0"
group "de.westnordost"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    jcenter()
}

dependencies {
    implementation 'org.json:json:20180813'
    testImplementation 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

bintray {
    user = bintray_user
    key = bintray_api_key
    publications = ['Library']
    pkg {
        repo = 'maven'
        name = 'osmfeatures'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/westnordost/osmfeatures.git'
        version {
            name = project.version
            released = new Date()
            gpg {
                sign = true
                passphrase = bintray_passphrase
            }
        }
    }
}

publishing {
    publications {
        Library(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifactId "osmfeatures"
            pom {
                name = 'osmfeatures'
                description = 'Java library to translate OSM tags to and from localized names.'
                url = 'https://github.com/westnordost/osmfeatures'
                scm {
                    connection = 'https://github.com/westnordost/osmfeatures.git'
                    developerConnection = 'https://github.com/westnordost/osmfeatures.git'
                    url = 'https://github.com/westnordost/osmfeatures'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'westnordost'
                        name = 'Tobias Zwick'
                        email = 'osm@westnordost.de'
                    }
                }
            }
        }
    }
}
